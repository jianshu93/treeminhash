//#######################################
//# Copyright (C) 2019-2020 Otmar Ertl. #
//# All rights reserved.                #
//#######################################

import java.security.MessageDigest

plugins {
    id "de.undercouch.download" version "3.4.3" // used for Download task
}

ext {
    paperDir = 'paper'
    pythonDir = 'python'
    cppDir = 'c++'
    bagCppDir = 'bagminhash/c++'
    dartCppDir = 'dartminhash'
    wyhashDir = "wyhash"
    dataDir = 'data'
    wyhashZipFile = "wyhash_v4.zip"
    wyhashCppDir = "${cppDir}/${wyhashDir}"
    wyhashSourceDir = "${wyhashCppDir}/wyhash-wyhash_v4"
    wyhashHeaderFile = "wyhash.h"
}

//#########################
// wyHash
//#########################

task downloadWyhash(type: Download) {
    src "https://github.com/wangyi-fudan/wyhash/archive/wyhash_v4.zip"
    dest "${wyhashCppDir}"
    overwrite true
    onlyIfModified true
}

task unzipWyhash(type: Copy) {
    inputs.files "${wyhashCppDir}/${wyhashZipFile}"
    def zipFile = file("${wyhashCppDir}/${wyhashZipFile}")
    def outputDir = file("${wyhashCppDir}")
    from zipTree(zipFile)
    into outputDir
    dependsOn downloadWyhash
}

task copyWyhashFiles(type: Copy) {
    from "${wyhashSourceDir}/${wyhashHeaderFile}"
    into "${wyhashCppDir}"
    dependsOn unzipWyhash
}

task getWyhash {
    group 'TreeMinHash'
    dependsOn copyWyhashFiles
}

//#########################
// tests
//#########################
   

task buildBitstreamTestExecutable(type: Exec) {
    inputs.files "${cppDir}/bitstream_test.cpp", "${cppDir}/bitstream_random.hpp"
    outputs.files "${cppDir}/bitstream_test.out"
    standardOutput = new ByteArrayOutputStream()
    commandLine 'g++','-O3','-std=c++17','-Wall',"${cppDir}/bitstream_test.cpp",'-o',"${cppDir}/bitstream_test.out"
}

task executeBitstreamTest (type: Exec) {
    inputs.files "${cppDir}/bitstream_test.out"
    commandLine "${cppDir}/bitstream_test.out","${dataDir}"
    dependsOn buildBitstreamTestExecutable
}

task buildRandomTestExecutable(type: Exec) {
    inputs.files "${cppDir}/random_test.cpp", "${cppDir}/bitstream_random.hpp","${cppDir}/exponential_distribution.hpp","${wyhashCppDir}/${wyhashHeaderFile}"
    outputs.files "${cppDir}/random_test.out"
    standardOutput = new ByteArrayOutputStream()
    commandLine 'g++','-O3','-std=c++17','-Wall','-DNDEBUG',"${cppDir}/random_test.cpp",'-o',"${cppDir}/random_test.out"
    //commandLine 'g++','-O3','-std=c++17','-Wall',"${cppDir}/random_test.cpp",'-o',"${cppDir}/random_test.out"
}

def randomTestFiles = [ \
    "${dataDir}/boolean.txt", \
    "${dataDir}/uniformLemire3.txt", \
    "${dataDir}/uniformLemire11.txt", \
    "${dataDir}/uniformLemire29.txt", \
    "${dataDir}/uniformLemire256.txt", \
    "${dataDir}/uniformLumbroso3.txt", \
    "${dataDir}/uniformLumbroso11.txt", \
    "${dataDir}/uniformLumbroso29.txt", \
    "${dataDir}/uniformLumbroso256.txt", \
    "${dataDir}/intPow3.txt", \
    "${dataDir}/intPow8.txt", \
    "${dataDir}/expZiggurat.txt", \
    "${dataDir}/expStandard.txt", \
    "${dataDir}/uniformDouble.txt", \
    "${dataDir}/uniformDoubleHalf.txt", \
    "${dataDir}/bernoulliReal0_2.txt", \
    "${dataDir}/bernoulliRatio1_3.txt", \
    "${dataDir}/truncatedExp0.txt", \
    "${dataDir}/truncatedExp0_1.txt", \
    "${dataDir}/truncatedExp0_5.txt", \
    "${dataDir}/truncatedExp1.txt", \
    "${dataDir}/truncatedExp2.txt" ]

def executeRandomTestOutput = "${dataDir}/random_test_calculation_times.txt"

task buildOrderMinhashTestExecutable(type: Exec) {
    inputs.files "${cppDir}/order_minhash_equivalence_test.cpp", "${cppDir}/minhash.hpp","${cppDir}/bitstream_random.hpp","${cppDir}/data_generation.hpp","${cppDir}/exponential_distribution.hpp","${wyhashCppDir}/${wyhashHeaderFile}"
    outputs.files "${cppDir}/order_minhash_equivalence_test.out"
    standardOutput = new ByteArrayOutputStream()
    commandLine 'g++','-O3','-std=c++17','-Wall', "${cppDir}/order_minhash_equivalence_test.cpp",'-o',"${cppDir}/order_minhash_equivalence_test.out"
}

task executeRandomTest (type: Exec) {
    inputs.files "${cppDir}/random_test.out"
    outputs.files randomTestFiles
    doFirst {
        standardOutput = new FileOutputStream(executeRandomTestOutput)
    }
    commandLine "${cppDir}/random_test.out","${dataDir}"
    dependsOn buildRandomTestExecutable
}

task performRandomTest (type: Exec) {
    inputs.files randomTestFiles, "${pythonDir}/random_test.py"
    outputs.files
    standardOutput = new ByteArrayOutputStream()
    commandLine 'python3', "${pythonDir}/random_test.py"
    dependsOn executeRandomTest
}

task performTests {
    group 'TreeMinHash'
    dependsOn performRandomTest, executeBitstreamTest
}

task buildPerformanceTestExecutable(type: Exec) {
    inputs.files \
        "${cppDir}/performance_test.cpp", 
        "${cppDir}/weighted_minwise_hashing.hpp",
        "${cppDir}/bitstream_random.hpp",
        "${cppDir}/data_generation.hpp",
        "${cppDir}/exponential_distribution.hpp",
        "${bagCppDir}/weighted_minwise_hashing.hpp",
        "${bagCppDir}/bitstream_random.hpp",
        "${bagCppDir}/data_generation.hpp",
        "${bagCppDir}/exponential_distribution.hpp",
        "${bagCppDir}/xxhash/xxhash.h",
        "${bagCppDir}/xxhash/libxxhash.a",
        "${dartCppDir}/dartminhash.hpp", 
        "${dartCppDir}/darthash.hpp", 
        "${dartCppDir}/similarity.hpp",
        "${dartCppDir}/hashing.hpp",
        "${wyhashCppDir}/${wyhashHeaderFile}",
        "${wyhashCppDir}/${wyhashHeaderFile}"
    outputs.files "$cppDir/performance_test.out"
    standardOutput = new ByteArrayOutputStream()
    commandLine 'g++','-O3','-march=native','-DNDEBUG','-std=c++17','-Wall',"${cppDir}/performance_test.cpp","${bagCppDir}/xxhash/libxxhash.a",'-o',"${cppDir}/performance_test.out"
    //commandLine 'g++','-O3','-march=native','-std=c++17','-Wall',"${cppDir}/performance_test.cpp","${bagCppDir}/xxhash/libxxhash.a",'-o',"${cppDir}/performance_test.out"
}

task buildErrorTestExecutable(type: Exec) {
    inputs.files \
        "${cppDir}/error_test.cpp", 
        "${cppDir}/weighted_minwise_hashing.hpp",
        "${cppDir}/bitstream_random.hpp",
        "${cppDir}/data_generation.hpp",
        "${cppDir}/exponential_distribution.hpp",
        "${bagCppDir}/weighted_minwise_hashing.hpp",
        "${bagCppDir}/bitstream_random.hpp",
        "${bagCppDir}/data_generation.hpp",
        "${bagCppDir}/exponential_distribution.hpp",
        "${bagCppDir}/xxhash/xxhash.h",
        "${bagCppDir}/xxhash/libxxhash.a",
        "${dartCppDir}/dartminhash.hpp", 
        "${dartCppDir}/darthash.hpp", 
        "${dartCppDir}/similarity.hpp",
        "${dartCppDir}/hashing.hpp",
        "${wyhashCppDir}/${wyhashHeaderFile}",
        "${wyhashCppDir}/${wyhashHeaderFile}"
    outputs.files "${cppDir}/error_test.out"
    standardOutput = new ByteArrayOutputStream()
    commandLine 'g++','-O3','-march=native', '-std=c++17','-fopenmp','-Wall',"${cppDir}/error_test.cpp","${bagCppDir}/xxhash/libxxhash.a",'-o',"${cppDir}/error_test.out"
    //commandLine 'g++','-O0','-std=c++17','-Wall',"${cppDir}/error_test.cpp","${bagCppDir}/xxhash/libxxhash.a",'-o',"${cppDir}/error_test.out"
}

def hashSizes = [256, 1024, 4096]
def dataSizes = [1, 2, 5, 10, 20, 50, 100, 200, 500, 1000, 2000, 5000, 10000, 20000, 50000, 100000, 200000, 500000, 1000000 /*, 2000000, 5000000, 10000000*/]

def generateMD5(String s) {
    MessageDigest digest = MessageDigest.getInstance("MD5")
    digest.update(s.bytes)
    new BigInteger(1, digest.digest()).longValue()
}

def performanceTestDatFiles = []
def performanceTestTasks = []

for(hashSize in hashSizes) {
    for(dataSize in dataSizes) {

        def performanceTestTaskName = "doPerformanceTest_${hashSize}_${dataSize}"
        def performanceTestDatFile = "$dataDir/performance_test_result_${hashSize}_${dataSize}.dat"
        def seed = generateMD5(performanceTestTaskName)

        task "$performanceTestTaskName" (type: Exec) {
            workingDir cppDir
            inputs.files "$cppDir/performance_test.out"
            outputs.files performanceTestDatFile
            doFirst {
                standardOutput = new FileOutputStream(performanceTestDatFile)
            }
            commandLine './performance_test.out', seed, hashSize, dataSize
            dependsOn 'buildPerformanceTestExecutable'
        }

        performanceTestDatFiles.add performanceTestDatFile
        performanceTestTasks.add performanceTestTaskName
    }
}

def executeErrorTestOutput = "${dataDir}/error_test.csv"



task executeErrorTest (type: Exec) {
    group 'TreeMinHash'
    inputs.files "${cppDir}/error_test.out"
    outputs.files executeErrorTestOutput
    doFirst {
        standardOutput = new FileOutputStream(executeErrorTestOutput)
    }
    commandLine "${cppDir}/error_test.out"
    dependsOn buildErrorTestExecutable
}

task executePerformanceTests {
    group 'TreeMinHash'
    dependsOn performanceTestTasks
    doLast {
    }
}

task execute {
    group 'TreeMinHash'
    dependsOn executePerformanceTests, executeErrorTest
    doLast {
    }
}

def speedChartsFig = "${paperDir}/speed_charts.pdf"
def errorChartsFig = "${paperDir}/error_charts.pdf"
def figFiles = [speedChartsFig, errorChartsFig]

task makeErrorFigures (type: Exec) {
    inputs.files executeErrorTestOutput, "${pythonDir}/error_charts.py","${pythonDir}/color_defs.py"
    outputs.files errorChartsFig
    doFirst {
        standardOutput = new ByteArrayOutputStream()
    }
    commandLine 'python3', "${pythonDir}/error_charts.py"
}

task makeSpeedTestFigures (type: Exec) {
    inputs.files performanceTestDatFiles, "${pythonDir}/speed_charts.py","${pythonDir}/color_defs.py"
    outputs.files speedChartsFig
    commandLine 'python3', "${pythonDir}/speed_charts.py"
}


task pdfFigures {
    dependsOn makeErrorFigures, makeSpeedTestFigures
}
